  //%attributes = {"lang":"en"} comment added and reserved by 4D.
C_LONGINT($choice;$run)
C_TEXT($path)
C_POINTER($sketch)
C_BLOB($pblob)

If (Shift down)
	WinChoice ("Are you sure you want to make this the label on D/N:"+[Samples]DesignNum+"?")
	If (ok=1)
		Case of 
			: ($1=1)
				vxSketch:=vxSketch1
			: ($1=2)
				vxSketch:=vxSketch2
			: ($1=3)
				vxSketch:=vxSketch3
			: ($1=4)
				vxSketch:=vxSketch4
		End case 
		VARIABLE TO BLOB(vxSketch;$pblob)
		  //OBJECT SET VISIBLE(vxSketch;True)
		[Designs]LabelPict:=True
		SAVE RECORD([Designs])
		$run:=Execute on server("Picture_LabelPut";32*1024;"LabelProc";[Samples]DesignNum;$pblob)
	End if 
Else 
	$choice:=Pop up menu("Larger Image;Get Image;Move to Desktop;Delete;Close Menu")
	$sketch:=Get pointer("vxSketch"+String($1))
	
	Case of 
		: ($choice=1)
			vxSketch:=$sketch->
			Center Window (300;300;1;"")
			DIALOG([Dialogs];"Print.i")
			CLOSE WINDOW
			
		: ($choice=2)
			Pix_GetImage ($1)
			Rev_LogEvent ("Image "+String($1)+" imported")
			
		: ($choice=3)
			$path:=Substring(Get 4D folder;1;Position("Library";Get 4D folder)-1)+"Desktop:"
			$path:=$path+String(Abs(<>vlOrderNumber))+"_"+String($1)+".jpg"
			WRITE PICTURE FILE($path;$sketch->;"JPEG")
			
		: ($choice=4)
			WinChoice ("Are you sure you want to delete this image?")
			If (ok=1)
				GET PICTURE FROM LIBRARY("empty.jpg";$sketch->)
				QUERY([SpecPix];[SpecPix]OrderNumber=<>vlOrderNumber)
				QUERY SELECTION([SpecPix];[SpecPix]SketchNumber=$1)
				DELETE RECORD([SpecPix])
				Rev_LogEvent ("Image "+String($1)+" deleted")
			End if 
	End case 
End if 
