  //%attributes = {"publishedWeb":true,"folder":"Default Project Methods","lang":"en"} comment added and reserved by 4D.
C_LONGINT($i;$oldKey)
Case of 
	: (Records in set("UserSet")=0)
		WinPrompt ("Select template to duplicate.")
	: (Records in set("UserSet")>1)
		WinPrompt ("You may duplicate only one template at a time.")
	Else 
		USE SET("UserSet")
		WinChoice ("Are you sure you want to duplicate the template: "+[SpecTemplate]Template)
		If (ok=1)
			$oldKey:=[SpecTemplate]TempKeyNumber
			DUPLICATE RECORD([SpecTemplate])
			[SpecTemplate]Template:=[SpecTemplate]Template+" (copy)"
			[SpecTemplate]TempKeyNumber:=Sequence number([SpecTemplate])
			SAVE RECORD([SpecTemplate])
			QUERY([SpecTempBuilder];[SpecTempBuilder]TempKey=$oldKey)
			Util_RecordNumberArray (Table(->[SpecTempBuilder]))
			
			$i:=1
			For ($i;1;Size of array(alRecordNumber))
				GOTO RECORD([SpecTempBuilder];alRecordNumber{$i})
				DUPLICATE RECORD([SpecTempBuilder])
				[SpecTempBuilder]TempKey:=[SpecTemplate]TempKeyNumber
				SAVE RECORD([SpecTempBuilder])
			End for 
		End if 
		QUERY([SpecTempBuilder];[SpecTempBuilder]TempKey=[SpecTemplate]TempKeyNumber)
		ORDER BY([SpecTempBuilder];[SpecTempBuilder]SortOrder;>)
		MODIFY RECORD([SpecTemplate];*)
End case 