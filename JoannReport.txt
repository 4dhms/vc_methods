  //%attributes = {"lang":"en"} comment added and reserved by 4D.
ARRAY REAL(arFOB;0)
ARRAY LONGINT(alOrders;0)

C_BOOLEAN($stop)
C_LONGINT($po;$i)
C_TIME($docref)
C_TEXT($path)

Repeat 
	$po:=Num(Request("HMS PO Number:"))
	If (ok=1)
		QUERY([Orders];[Orders]OrderNum=$po)
		Case of 
			: (Find in array(alOrders;$po)>0)
				ALERT(String($po)+" has been entered already.")
			: (Records in selection([Orders])=0)
				ALERT(String($po)+" is not a valid purchase order.")
			Else 
				APPEND TO ARRAY(alOrders;$po)
		End case 
	Else 
		$stop:=True
	End if 
Until ($stop)

If (Size of array(alOrders)>0)
	$docref:=Create document("";"xls")
	If (ok=1)
		$path:=Document
		$i:=1
		For ($i;1;Size of array(alOrders))
			QUERY([Orders];[Orders]OrderNum=alOrders{$i})
			QUERY([CostSheets];[Costing]OrderNumber=[Orders]OrderNum)
			ORDER BY([CostSheets];[CostSheets]SheetID;<)
			SEND PACKET($docref;String([Orders]OrderNum)+Char(9)+String([CostSheets]FOB;"##0.00")+Char(13))
		End for 
		CLOSE DOCUMENT($docref)
		SHOW ON DISK($path)
	End if 
End if 

