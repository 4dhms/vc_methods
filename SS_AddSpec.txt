  //%attributes = {"publishedWeb":true,"folder":"Specsheets","lang":"en"} comment added and reserved by 4D.
C_LONGINT($partNumber;$i)
C_TEXT($thisSet)

$partNumber:=SS_PartNumber 

If (Records in selection([SpecSheetLines])=Records in set("AllLines"))
	$thisSet:="AllLines"
Else 
	$thisSet:="ActiveLines"
End if 

FIRST RECORD([SpecSheetLines])
Spec_POMArray 

vsRequestString:=""
Center Window (600;550;1;"")
DIALOG([Dialogs];"AddSpec")
CLOSE WINDOW

If (ok=1)
	$i:=1
	If (vsRequestString#"")
		INSERT IN ARRAY(asPart;1;1)
		asPart{1}:=vsRequestString
	End if 
	
	For ($i;1;Size of array(asPart))
		CREATE RECORD([SpecSheetLines])
		[SpecSheetLines]SF_KeyNumber:=[SpecFactory]SF_KeyNumber
		If ($i=1) & (vsRequestString#"")
			[SpecSheetLines]OtherDescription:=vsRequestString
		Else 
			QUERY([ChineseDictionary];[ChineseDictionary]EnglishName=asPart{$i})
			[SpecSheetLines]SpecLibKey:=[ChineseDictionary]LexKey
			[SpecSheetLines]OtherDescription:=[ChineseDictionary]EnglishName
		End if 
		[SpecSheetLines]SpecLineKey:=Sequence number([SpecSheetLines])
		[SpecSheetLines]Spec1A:=vsMeasure
		  //[SpecSheetLines]Tolerance:=vtTolerance
		[SpecSheetLines]PartNumber:=$partNumber
		[SpecSheetLines]SortOrder:=Records in set("AllLines")+1
		SAVE RECORD([SpecSheetLines])
		ADD TO SET([SpecSheetLines];"AllLines")
		ADD TO SET([SpecSheetLines];"ActiveLines")
		USE SET($thisSet)
		QUERY SELECTION([SpecSheetLines];[SpecSheetLines]PartNumber=$partNumber)
		ORDER BY([SpecSheetLines];[SpecSheetLines]SortOrder;>)
	End for 
End if 