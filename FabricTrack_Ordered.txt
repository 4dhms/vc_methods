  //%attributes = {"lang":"en"} comment added and reserved by 4D.
C_LONGINT($i;$s;$c;$n;$totstart;$totend;$subtot)
C_TEXT($testcolor)
C_POINTER($thiscol)

$s:=Size of array(asMOrder)
FabricTrack_ArrayInsert ($s+1;2)
$s:=$s+2
asMOrder{$s}:="Ordered"
$totstart:=$s

For ($i;1;Size of array(alFabOrderKey))
	QUERY([FabPOLines];[FabPOLines]FabOrderKey=alFabOrderKey{$i})
	QUERY([FabricOrders];[FabricOrders]FabOrderKey=alFabOrderKey{$i})
	
	$s:=$s+1
	FabricTrack_ArrayInsert ($s;1)
	asMOrder{$s}:=[FabricOrders]PONumber
	
	FIRST RECORD([FabPOLines])
	$n:=1
	For ($n;1;Records in selection([FabPOLines]))
		Case of 
			: ([FabPOLines]ColorPrint="Black")
				$testcolor:="8619"
			: ([FabPOLines]ColorPrint="White")
				$testcolor:="8619"
			Else 
				$testcolor:=Replace string([FabPOLines]ColorPrint;"#";"")
		End case 
		
		If (Position(" ";$testcolor)>0)
			$testcolor:=Substring($testcolor;1;Position(" ";$testcolor)-1)
		End if 
		
		$c:=Find in array(asColors;$testcolor)
		
		If ($c>0)
			$thiscol:=Get pointer("arMcol"+String($c))
			$thiscol->{$s}:=[FabPOLines]Quantity
			alFabricColor{$s}:=16777215
		End if 
		NEXT RECORD([FabPOLines])
	End for 
End for 

$s:=$s+1
FabricTrack_ArrayInsert ($s;1)
asFactories{$s}:="Total Ordered"
$totend:=$s
$s:=$s+1

  //orders shipped
$i:=1
For ($i;1;Size of array(asColors))
	$thisCol:=Get pointer("arMCol"+String($i))
	$n:=1
	$subtot:=0
	For ($n;1;$totend-$totstart)
		$subtot:=$thisCol->{$totstart+$n}+$subtot
	End for 
	$thisCol->{$totend}:=$subtot
	APPEND TO ARRAY(alTrackOrdered;$subtot)
End for 

