  //%attributes = {"lang":"en"} comment added and reserved by 4D.
C_LONGINT($i;$temp;$n;$s;$c;$p;$qty;$totqty;$subqty;$sscount;$stylecount;$sstot;$prod;$totprod)
C_LONGINT($pix;$page;$onumber)
C_TEXT($prevdiv;$path;$color;$thisseason;$factory;$strip;$year)
C_TEXT($start;$end;$division;$seasonbuilder;$season)
C_POINTER($part)

$pix:=0
$page:=1

  //ARRAY TEXT($checker;0)
ARRAY TEXT($seasons;0)
  //

WinChoice ("What are you reporting?";"Regina/TD";"Glory")
If (ok=1)
	$factory:="Regina@"
Else 
	$factory:="Glory@"
End if 

$year:=Request("What year do you want?";String(Year of(Current date(*))))

$i:=1
For ($i;1;8)
	Case of 
		: ($i=1)
			$season:="Fall "+$year
		: ($i=2)
			$season:="Holiday "+$year
		: ($i=3)
			$season:="Resort "+$year
		: ($i=4)
			$season:="Spring "+$year
		: ($i=5)
			$season:="Spring II "+$year
		: ($i=6)
			$season:="Summer "+$year
		: ($i=7)
			$season:="Transition "+$year
		: ($i=8)
			$season:="Downtime "+$year
	End case 
	WinChoice ("Add "+$season+" to report?")
	If (ok=1)
		APPEND TO ARRAY($seasons;$season)
	End if 
End for 

QUERY([Samples];[Samples]Void=False)
QUERY SELECTION([Samples];[Samples]SeasonYear=$year)

Case of 
	: (Size of array($seasons)=1)
		$thisseason:=Replace string($seasons{1};" "+$year;"")
		$seasonbuilder:=$seasons{1}
		QUERY SELECTION([Samples];[Samples]SeasonName=$thisseason)
	Else 
		$i:=1
		For ($i;1;Size of array($seasons))
			Case of 
				: ($i=1)
					$thisseason:=Replace string($seasons{$i};" "+$year;"")
					$seasonbuilder:=$seasons{1}
					QUERY SELECTION([Samples];[Samples]SeasonName=$thisseason;*)
				: ($i=Size of array($seasons))
					$thisseason:=Replace string($seasons{$i};" "+$year;"")
					$seasonbuilder:=$seasonbuilder+" & "+$seasons{$i}
					QUERY SELECTION([Samples]; | ;[Samples]SeasonName=$thisseason)
				Else 
					$thisseason:=Replace string($seasons{$i};" "+$year;"")
					$seasonbuilder:=$seasonbuilder+", "+$seasons{$i}
					QUERY SELECTION([Samples]; | ;[Samples]SeasonName=$thisseason;*)
			End case 
		End for 
End case 


Case of 
	: ($factory="Glory@")
		QUERY SELECTION([Samples];[Samples]SamGarFactry=$factory)
	: ($factory="Regina@")
		QUERY SELECTION([Samples];[Samples]SamGarFactry=$factory;*)
		QUERY SELECTION([Samples]; | ;[Samples]SamGarFactry="TD";*)
		QUERY SELECTION([Samples]; | ;[Samples]SamGarFactry="TD@Regina")
End case 

If ($factory="Glory@")
	WinChoice ("Report division:";"03-Cupio";"06-C&G")
	If (ok=1)
		$division:="03-Cupio"
		QUERY SELECTION([Samples];[Samples]SampleDivision="03@")
	Else 
		$division:="06-Cable & Gauge"
		QUERY SELECTION([Samples];[Samples]SampleDivision="06@")
	End if 
Else 
	QUERY SELECTION([Samples];[Samples]SampleDivision="03-Cupio@";*)
	QUERY SELECTION([Samples]; | ;[Samples]SampleDivision="04-Cupio@")
End if 

QUERY SELECTION([Samples];[Samples]StyleNum#"")

ARRAY TEXT($styles;0)
ARRAY TEXT($designs;0)
ARRAY DATE($date;0)
ARRAY TEXT($ss;0)
ARRAY LONGINT($ordercount;0)
ARRAY TEXT($cp;0)
ARRAY LONGINT($quantity;0)

If (Records in selection([Samples])=0)
	WinPrompt ("Nothing to report.")
Else 
	WinChoice ("Do you want to report Used or UNused style numbers?";"Used";"Unused")
	
	If (ok=1)
		$i:=1
		ARRAY TEXT($rawS;0)
		ARRAY TEXT($rawD;0)
		SELECTION TO ARRAY([Samples]StyleNum;$rawS)
		SELECTION TO ARRAY([Samples]DesignNum;$rawD)
		
		For ($i;1;Size of array($rawS))
			$strip:=StripToNumber ($rawS{$i})
			If (Find in array($styles;$strip)<1)
				APPEND TO ARRAY($styles;$strip)
			End if 
			$strip:=StripToNumber ($rawD{$i})
			If (Find in array($designs;$strip)<1)
				APPEND TO ARRAY($designs;$strip)
			End if 
		End for 
		
		CREATE SET([Samples];"report")
		$i:=1
		$stylecount:=0
		$sscount:=0
		$sstot:=0
		$prod:=0
		
		ARRAY TEXT($usedstyles;0)
		For ($i;1;Size of array($styles))
			
			$subqty:=0
			USE SET("report")
			QUERY SELECTION([Samples];[Samples]StyleNum="@"+$styles{$i})
			$stylecount:=$stylecount+Records in selection([Samples])
			QUERY SELECTION([Samples];[Samples]Sam Type="Sales Sample")
			$sstot:=$sstot+Records in selection([Samples])
			If (Records in selection([Samples])>0)
				$sscount:=$sscount+1
			End if 
			$p:=1
			FIRST RECORD([Samples])
			CREATE EMPTY SET([Orders];"ord")
			
			For ($p;1;Records in selection([Samples]))
				$strip:=StripToNumber ([Samples]DesignNum)+"@"
				QUERY([Orders];[Orders]DesignNumber=$strip)
				QUERY SELECTION([Orders];[Orders]Cancelled=False)
				
				$s:=1
				FIRST RECORD([Orders])
				For ($s;1;Records in selection([Orders]))
					ADD TO SET([Orders];"ord")
					If (Find in array($usedstyles;StripToNumber ([Orders]StyleNumber))<1)
						APPEND TO ARRAY($usedstyles;StripToNumber ([Orders]StyleNumber))
					End if 
					NEXT RECORD([Orders])
				End for 
				NEXT RECORD([Samples])
			End for 
			USE SET("ord")
			CLEAR SET("ord")
			
			If (Records in selection([Orders])>0) & ([Orders]StyleNumber#"")
				  //If (Find in array($usedstyles;StripToNumber ([Orders]StyleNumber))<1)
				  //APPEND TO ARRAY($usedstyles;StripToNumber ([Orders]StyleNumber))
				  //End if 
				$prod:=$prod+1
			End if 
		End for 
		
		
		  //SEND PACKET($docref;"Used in Production"+Char(9)+String($prod)+Char(13)+Char(13))
		  //SEND PACKET($docref;"% of Styles with SS"+Char(9)+String(Round($prod/$sscount;2)*100)+"%"+Char(13)+Char(13))
		  //SEND PACKET($docref;"% of All Styles"+Char(9)+String(Round(($prod/Size of array($styles));2)*100)+"%"+Char(13)+Char(13))
		
		$totqty:=0
		
		$i:=1
		  //SEND PACKET($docref;"Style"+Char(9)+"No of Orders"+Char(9)+"Color/Print"+Char(9)+"Quantity"+Char(9)+"Total"+Char(13))
		  //If (Size of array($usedstyles)=0)
		  //WinPrompt ("No styles used yet.")
		  //Else 
		If (Size of array($usedstyles)>0)
			PRINT SETTINGS
			
			For ($i;1;Size of array($usedstyles))
				If ($pix<48)
					If ($i>1)
						PAGE BREAK(>)
					End if 
					vsPart1:=$division+" "+$seasonbuilder
					vsPart2:="Page "+String($page)
					$page:=$page+1
					Print form("Glory_Header")
					$pix:=660
				End if 
				
				If ($i=1)
					$c:=1
					For ($c;1;15)
						$part:=Get pointer("vsPart"+String($c))
						$part->:=""
					End for 
					vsPart1:="Total Styles"
					vsPart3:=String(Size of array($styles))
					Print form("Glory_Stats")
					  //vsPart1:="Protos"
					  //If ($division="03@")
					  //vsPart3:="274"
					  //Else 
					  //vsPart3:="263"
					  //End if 
					  //Print form("Glory_Stats")
					vsPart1:="Styles with Sales Samples"
					vsPart3:=String($sstot)
					Print form("Glory_Stats")
					vsPart1:="Used in Production"
					vsPart3:=String($prod)
					Print form("Glory_Stats")
					vsPart1:="% of Styles with SS"
					vsPart3:=String(Round($prod/$sstot;2)*100)+"%"
					Print form("Glory_Stats")
					Print form("Glory_Space")
					Print form("Glory_Space")
					$pix:=$pix-90
				End if 
				
				ARRAY LONGINT($ordercount;0)
				ARRAY TEXT($cp;0)
				ARRAY LONGINT($quantity;0)
				
				QUERY([Orders];[Orders]StyleNumber="@"+$usedstyles{$i})
				APPEND TO ARRAY($ordercount;Records in selection([Orders]))
				FIRST RECORD([Orders])
				
				Case of 
					: (Records in selection([Orders])=1)
						QUERY([CP_OrderXRef];[CP_OrderXRef]OrderNumber=[Orders]OrderNum)
						QUERY SELECTION([CP_OrderXRef];[CP_OrderXRef]CPType="Self")
					: (Records in selection([Orders])=2)
						ARRAY LONGINT($ordernumbers;0)
						SELECTION TO ARRAY([Orders]OrderNum;$ordernumbers)
						QUERY([CP_OrderXRef];[CP_OrderXRef]OrderNumber=$ordernumbers{1};*)
						QUERY([CP_OrderXRef]; | ;[CP_OrderXRef]OrderNumber=$ordernumbers{2})
						QUERY SELECTION([CP_OrderXRef];[CP_OrderXRef]CPType="Self")
					Else 
						ARRAY LONGINT($ordernumbers;0)
						SELECTION TO ARRAY([Orders]OrderNum;$ordernumbers)
						$s:=1
						For ($s;1;Size of array($ordernumbers))
							Case of 
								: ($s=1)
									QUERY([CP_OrderXRef];[CP_OrderXRef]OrderNumber=$ordernumbers{1};*)
								: ($s=Size of array($ordernumbers))
									QUERY([CP_OrderXRef]; | ;[CP_OrderXRef]OrderNumber=$ordernumbers{$s})
								Else 
									QUERY([CP_OrderXRef]; | ;[CP_OrderXRef]OrderNumber=$ordernumbers{$s};*)
							End case 
						End for 
				End case 
				QUERY SELECTION([CP_OrderXRef];[CP_OrderXRef]CPType="Self")
				
				$c:=1
				$subqty:=0
				ARRAY LONGINT($self;0)
				DISTINCT VALUES([CP_OrderXRef]SelfKey;$self)
				For ($c;1;Size of array($self))
					QUERY([CP_OrderXRef];[CP_OrderXRef]SelfKey=$self{$c})
					QUERY SELECTION([CP_OrderXRef];[CP_OrderXRef]CPType="Self";*)
					QUERY SELECTION([CP_OrderXRef]; | ;[CP_OrderXRef]CPType="Combo 1")
					ORDER BY([CP_OrderXRef];[CP_OrderXRef]CPType;<)
					
					QUERY([ColorPrintNumber];[ColorPrintNumber]CPNumber=[CP_OrderXRef]CPNumber)
					$color:=[CP_OrderXRef]CPNumber+" "+[ColorPrintNumber]CPName
					$qty:=[CP_OrderXRef]Quantity
					$totqty:=$totqty+$qty
					$subqty:=$subqty+$qty
					If (Records in selection([CP_OrderXRef])>1)
						NEXT RECORD([CP_OrderXRef])
						QUERY([ColorPrintNumber];[ColorPrintNumber]CPNumber=[CP_OrderXRef]CPNumber)
						$color:=$color+"/"+[CP_OrderXRef]CPNumber+" "+[ColorPrintNumber]CPName
					End if 
					APPEND TO ARRAY($cp;$color)
					APPEND TO ARRAY($quantity;$qty)
				End for 
				
				$c:=1
				For ($c;1;15)
					$part:=Get pointer("vsPart"+String($c))
					$part->:=""
				End for 
				
				If (Size of array($cp)>0)
					vsPart1:=$usedstyles{$i}
					vsPart2:=String($ordercount{1})
					vsPart3:=$cp{1}
					vsPart4:=String($quantity{1};"###,###")
					vsPart5:=String($subqty;"###,###")
					QUERY([Orders];[Orders]StyleNumber="@"+vsPart1)
					QUERY([Designs];[Designs]DesignNum=[Orders]DesignNumber)
					vxSketch:=[Designs]Sketch
					
					If (Size of array($cp)>1)
						vsPart8:=$cp{2}
						vsPart9:=String($quantity{2};"###,###")
						If (Size of array($cp)>2)
							vsPart13:=$cp{3}
							vsPart14:=String($quantity{3};"###,###")
						End if 
					End if 
				End if 
				If (vsPart1#"")
					Print form("Glory_Color")
				End if 
				$pix:=$pix-36
				
				Case of 
					: (Size of array($cp)=3)
						Print form("Glory_Space")
						$pix:=$pix-6
					: (Size of array($cp)>3)
						$c:=1
						For ($c;1;Size of array($cp)-3)
							vsPart1:=""
							vsPart2:=String($cp{$c+3})
							vsPart3:=$cp{$c+3}
							vsPart4:=String($quantity{$c+3};"###,###")
							vsPart5:=""
							Print form("Glory_ColorA")
							$pix:=$pix-13
						End for 
						Print form("Glory_Space")
						$pix:=$pix-6
				End case 
			End for 
			Print form("Glory_Space")
			Print form("Glory_Space")
			Print form("Glory_Space")
			$c:=1
			For ($c;1;15)
				$part:=Get pointer("vsPart"+String($c))
				$part->:=""
			End for 
			vsPart1:="Grand Total"
			vsPart5:=String($totqty;"###,###")
			Print form("Glory_ColorA")
			
			PAGE BREAK
		Else 
			WinPrompt ("No styles used yet")
		End if 
	Else 
		vtTitle:=$division+" "+$seasonbuilder+" UNUSED"
		QUERY SELECTION([Samples];[Samples]Sam Type="Sales Sample")
		Report_GloryUnused 
	End if 
End if 
  //End if 

