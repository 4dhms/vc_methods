  //%attributes = {"lang":"en"} comment added and reserved by 4D.
QUERY([Orders];[Orders]PODate>=!2014-01-01!;*)
QUERY([Orders]; & ;[Orders]PODate<!2015-01-01!;*)
QUERY([Orders]; & ;[Orders]Cancelled=False)
CREATE SET([Orders];"vector")

C_LONGINT($i)
C_TIME($docref)
C_TEXT($path)


ARRAY TEXT($vendors;0)
DISTINCT VALUES([Orders]Factory;$vendors)
SORT ARRAY($vendors;>)

$i:=1
ARRAY LONGINT($vol;Size of array($vendors))
ARRAY LONGINT($units;Size of array($vendors))

For ($i;1;Size of array($vendors))
	USE SET("vector")
	QUERY SELECTION([Orders];[Orders]Factory=$vendors{$i})
	$vol{$i}:=Records in selection([Orders])
	$units{$i}:=Sum([Orders]TotalOrderQuantity)
End for 

$docref:=Create document("";"xls")

If (ok=1)
	$path:=Document
	$i:=1
	For ($i;1;Size of array($vendors))
		SEND PACKET($docref;$vendors{$i}+Char(9)+String($vol{$i})+Char(9)+String($units{$i})+Char(13))
	End for 
	
	CLOSE DOCUMENT($docref)
	SHOW ON DISK($path)
End if 
