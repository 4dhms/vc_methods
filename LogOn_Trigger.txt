  //%attributes = {"lang":"en"} comment added and reserved by 4D.
C_TEXT($style;$num;$name)
C_LONGINT($i;$n;$prev;$bg)

ARRAY LONGINT(alOrders;0)
ARRAY TEXT(asCustomers;0)
ARRAY REAL(arPrice;0)
ARRAY TEXT(asCPNumbers;0)
ARRAY TEXT(asCPColorName;0)
ARRAY TEXT(asCC;0)

Case of 
	: (Form event=On Double Clicked) | (FORM Get current page=2)
		$style:=asStyleNumbers{asStyleNumbers}
		
		QUERY([Orders];[Orders]StyleNumber=$style)
		$i:=1
		FIRST RECORD([Orders])
		For ($i;1;Records in selection([Orders]))
			QUERY([CP_OrderXRef];[CP_OrderXRef]OrderNumber=[Orders]OrderNum)
			QUERY SELECTION([CP_OrderXRef];[CP_OrderXRef]CPType="Self")
			ARRAY LONGINT($self;0)
			SELECTION TO ARRAY([CP_OrderXRef]CPOrdID;$self)
			
			$n:=1
			For ($n;1;Size of array($self))
				QUERY([CP_OrderXRef];[CP_OrderXRef]CPOrdID=$self{$n})
				$num:=[CP_OrderXRef]CPNumber
				QUERY([ColorPrintNumber];[ColorPrintNumber]CPNumber=[CP_OrderXRef]CPNumber)
				$name:=[ColorPrintNumber]CPName
				
				  //looks for combos
				QUERY([CP_OrderXRef];[CP_OrderXRef]SelfKey=$self{$n})
				QUERY SELECTION([CP_OrderXRef];[CP_OrderXRef]CPType="Combo 1")
				If (Records in selection([CP_OrderXRef])>0)
					$num:=$num+"/"+[CP_OrderXRef]CPNumber
					QUERY([ColorPrintNumber];[ColorPrintNumber]CPNumber=[CP_OrderXRef]CPNumber)
					$name:=$name+"/"+[ColorPrintNumber]CPName
				End if 
				
				APPEND TO ARRAY(alOrders;[Orders]OrderNum)
				APPEND TO ARRAY(asCustomers;[Orders]Customer)
				APPEND TO ARRAY(arPrice;[Orders]Price)
				APPEND TO ARRAY(asCPNumbers;$num)
				APPEND TO ARRAY(asCPColorName;$name)
				If ([CP_OrderXRef]ColorCodeOverride)
					APPEND TO ARRAY(asCC;[CP_OrderXRef]ColorCodeNew)
				Else 
					APPEND TO ARRAY(asCC;[CP_OrderXRef]ColorCode)
				End if 
			End for 
			NEXT RECORD([Orders])
		End for 
		
		$prev:=alOrders{1}
		ARRAY LONGINT(alShadeFab;Size of array(alOrders))
		
		$i:=1
		For ($i;1;Size of array(alOrders))
			Case of 
				: ($i=1)
					$bg:=16777215
				: (alOrders{$i}#alOrders{$i-1})
					If ($bg=16777215)
						$bg:=15461360
					Else 
						$bg:=16777215
					End if 
			End case 
			
			alShadeFab{$i}:=$bg
		End for 
		
		LogOn_ShowChanges 
		
		If (asStyleNumbers=1)
			OBJECT SET ENABLED(b2;False)
		Else 
			OBJECT SET ENABLED(b2;True)
		End if 
		
		If (asStyleNumbers=Size of array(asStyleNumbers))
			OBJECT SET ENABLED(b3;False)
		Else 
			OBJECT SET ENABLED(b3;True)
		End if 
		
		If (asStyleNumbers<1)
			vsFlag:=""
		Else 
			vsFlag:="S/N: "+asStyleNumbers{asStyleNumbers}
		End if 
		
		FORM GOTO PAGE(2)
End case 

